Actor GrenadeTimer : Inventory { Inventory.MaxAmount 90 }

ACTOR HandGrenade : Weapon
{
	Obituary "%k > Hand Grenade > %o"
	Weapon.AmmoType "HandGrenadeAmmo"
	Weapon.AmmoGive 1
	Weapon.AmmoUse 1
	+WEAPON.NOAUTOAIM
	+WEAPON.NOALERT
	+WEAPON.NOAUTOFIRE
	+INVENTORY.UNDROPPABLE
	States
	{
	Ready:
		GREN A 1 A_WeaponReady
		Loop
	Deselect:
		GREN A 0 A_Lower
		GREN A 1 A_Lower
		Loop
	Select:
		GREN A 0 A_Raise
		GREN A 1 A_Raise
		Loop
	Fire:
		GRNF A 4
		GRNF B 4 A_PlaySound("Grenade/PullPin")
		GRNF C 4
		GRNF A 0 A_GiveInventory("GrenadeTimer", 90)
		GRNF A 0 A_ReFire
		Goto Throw
	Throw:
		GRNF D 3 A_PlaySound("Grenade/Toss")
		GRNF E 3
		GRNF F 3 A_FireCustomMissile("ThrownGrenade",0,1,0,0)
		GRNF A 0 A_TakeInventory("GrenadeTimer", 90)
		Goto Ready
	Hold:
		GRNF C 1
		GRNF A 0 A_TakeInventory("GrenadeTimer", 1)
		GRNF A 0 A_ReFire
		Goto Throw
	}
}

ACTOR ThrownGrenade
{
	Radius 5
	Height 5
	Speed 24
	Damage 20
	SeeSound "weapons/grnbounce"
	PROJECTILE
	-NOGRAVITY
	+HEXENBOUNCE
	+LOWGRAVITY
	States
	{
	Spawn:
		GRND A 0
		GRND A 4 A_AlertMonsters
		GRND BCDEFGH 4
		Goto Death
	Death:
		MISL B 0 A_PlaySound("weapons/grnexplode")
		MISL B 0 A_Explode
		MISL BCD 3 Bright
		Stop
	}
}

Actor GrenadeLauncher : Weapon
{
	Radius 20
	Height 16
	+WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	+INVENTORY.UNDROPPABLE
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "HandGrenadeAmmo"
	Weapon.Kickback 100
	Obituary "%k > Grenade Launcher > %o"
	States
	{
	Ready:
		GRLG A 1 A_WeaponReady
		Loop
	Deselect:
		GRLG A 0 A_Lower
		GRLG A 1 A_Lower
		Loop
	Select:
		GRLG A 0 A_Raise
		GRLG A 1 A_Raise
		Loop
	Fire:
		GRLG B 8 A_GunFlash
		GRLG B 12 A_FireCustomMissile("STGrenade", 0, 1, 0, 0, 0, 6.328125)
		GRLG B 0 A_ReFire
		Goto Ready 
	Flash: 
		GRLF A 3 Bright A_Light1
		GRLF B 4 Bright
		GRLF C 4 Bright A_Light2
		GRLF D 4 Bright A_Light2
		Goto LightDone
	}
}

Actor STGrenade
{
	Radius 8
	Height 8
	Speed 25
	Damage 20
	Gravity 0.25
	Projectile
	-NoGravity
	+DoomBounce
	+DehExplosion
	+GrenadeTrail
	+ExplodeOnDeath
	SeeSound "Weapons/GrBnce"
	DeathSound "Weapons/GrenLX"
	DamageType Fire
	States
	{
	Spawn:
		SGRN A 0 Bright
		SGRN A 0 Bright A_StopSound
		SGRN A 0 Bright A_PlaySound("Weapons/GrenLF")
		SGRN A 1 Bright 
		Goto Spawn+3
	Death:
		MISL B 0 Bright A_ChangeFlag("NoGravity", 1)
		MISL B 8 Bright A_Explode
		MISL C 6 Bright
		MISL D 4 Bright
		stop
	}
}

actor 40mmGrenadeLauncher : Weapon 29010
{
	weapon.ammogive 10
	weapon.ammotype1 "40mmGrenades"
	weapon.ammotype2 "40mmGrenades"
	weapon.ammouse1 1
	weapon.ammouse2 2
	+WEAPON.EXPLOSIVE
	+INVENTORY.UNDROPPABLE
	Obituary "%k > 40mm Grenade Launcher > %o"
	states
	{
	Ready:
		RGLG A 1 A_WeaponReady
	Select:
		RGLG A 0 A_Raise
		RGLG A 1 A_Raise
		loop
	Deselect:
		RGLG A 0 A_Lower
		RGLG A 1 A_Lower
		loop
	Fire:
		RGLF A 3
		RGLF A 0 A_PlayWeaponSound ("40mm/grenfire")
		RGLF B 3
		RGLF B 0 A_FireCustomMissile ("40mmShot", 0, 1, 1, 10, 1)
		RGLG ABC 3
		RGLG A 15
		Goto Ready
	AltFire:
		RGLF A 3
		RGLF A 0 A_PlayWeaponSound ("40mm/grenfire")
		RGLF B 3
		RGLF B 0 A_FireCustomMissile ("40mmTimedShot", 0, 1, 1, 10, 1)
		RGLG ABC 3
		RGLG A 30
		Goto Ready
	}
}

actor 40mmShot
{
	radius 8
	height 8
	damage 25
	speed 50
	Scale 0.75
	seesound "40mm/grenbounce"
	deathsound "40mm/grenexplode"
	PROJECTILE
	-NOGRAVITY
	states
	{
	Spawn:
		SHRP O 3 bright A_SpawnItemEX("GrenadeSmoke",0,0,0,0,0,0,0,2)
		SHRP O 0 A_CountDown
		loop
	Death:
		MIS6 B 3 bright A_SetTranslucent (0.85,1)
		MIS6 C 3 bright A_Explode
		MIS6 DEF 3 bright 
		stop
	}
}

actor 40mmTimedShot : 40mmShot
{
	reactiontime 30
	Speed 30
	Damage 1
	+HEXENBOUNCE
	+BOUNCEONACTORS
	+EXTREMEDEATH
	+CANBOUNCEWATER
	states
	{
	Death:
		MIS6 B 3 bright A_SetTranslucent (0.85,1)
		MIS6 C 3 bright A_Explode (200, 200, 0)
		MIS6 DEF 3 bright 
		stop
	}
}

ACTOR GrenadeSmoke
{   
	Radius 0
	Height 1
	Speed 0
	PROJECTILE
	scale 0.75
	RENDERSTYLE Translucent
	ALPHA 0.45
	States
	{
	Spawn:
		NULL A 3 bright
		SMK2 ABCDE 2 Bright
		Stop 
	}
}

Actor DGL : Weapon
{
	Weapon.AmmoType "RocketAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	+Weapon.Explosive
	+Weapon.NoAutoFire
	Obituary "%k > Double Grenade Launcher > %o"
	+INVENTORY.UNDROPPABLE
	States
	{
	Select:
		DGLG B 0 A_Raise
		DGLG B 1 A_Raise
		Loop
	Deselect:
		DGLG B 0 A_Lower
		DGLG B 1 A_Lower
		Loop
	Ready:
		DGLG B 1 A_WeaponReady
		Loop
	Fire:
		DGLG C 3 Bright
		DGLG D 0 Bright A_PlayWeaponSound("DGL/Fire")
		DGLG D 0 Bright A_FireCustomMissile("DGrenade", Random(-1, -5), 1, 3)
		DGLG D 3 Bright A_FireCustomMissile("DGrenade", Random(1, 5), 1, -3)
		DGLG E 4 Bright
		DGLG F 5
		DGLG B 10
		Goto Ready
	}
}

Actor DGrenade
{
	Radius 6
	Height 8
	Mass 1
	Speed 30
	Damage 10
	SeeSound "DGL/Bounce"
	Projectile
	+DoomBounce
	-NoGravity
	+GrenadeTrail
	+DontFall
	+CanBounceWater
	BounceFactor 0.5
	BounceCount 20
	DamageType Fire
	States
	{
	Spawn:
		DGRE AAA 35 Bright
	Death:
		MISL B 0 Bright A_PlaySound("Weapons/RockLX")
		MISL B 0 Bright A_ChangeFlag("NoGravity", 1)
		MISL B 4 Bright A_Explode
		MISL C 5 Bright
		MISL D 6 Bright
		Stop
	}
}

Actor KDiZDGrenadeLauncher : Weapon
{
	Weapon.AmmoType "RocketAmmo"
	Weapon.AmmoGive 2
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "RocketAmmo"
	Weapon.AmmoUse2 1
	Weapon.Kickback 600
	+INVENTORY.UNDROPPABLE
	Obituary "%k > KDiZD's Grenade Launcher > %o"
	States
	{
	Select:
		GLAG A 0 A_Raise
		GLAG A 1 A_Raise
		Loop
	Deselect:
		GLAG A 0 A_Lower
		GLAG A 1 A_Lower
		Loop
	Ready:
		GLAG A 1 A_WeaponReady
		Loop
	Fire:
		GLAG B 0 Bright A_GunFlash
		GLAG B 0 Bright A_PlaySound("Weapons/GrenadeFire")
		GLAG B 4 Bright A_FireCustomMissile("KDiZDGrenade", 0, 1, 0, 0)
		GLAG I 4
		GLAG A 0 A_CheckReload
		GLAG C 7
		GLAG D 5
		GLAG E 5 A_PlaySound("Weapons/GrenadeOpen")
		GLAG FG 5
		GLAG H 5 A_PlaySound("Weapons/GrenadeLoad")
		GLAG FE 5
		GLAG D 5 A_PlaySound("Weapons/GrenadeClose")
		GLAG A 3
		GLAG A 0 A_Refire
		Goto Ready
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 2 A_Light2
		TNT1 A 0 A_Light0
		Stop
	}
}

Actor KDiZDGrenade
{
	Radius 11
	Height 8
	Speed 35	
	Damage 13 //(15*random(1,16))
	PROJECTILE
	+HEXENBOUNCE
	-NOGRAVITY
	+EXPLODEONWATER
	BounceFactor 0.8
	BounceCount 6
	ReactionTime 139
	Seesound "Weapons/GrenadeBounce"
	Deathsound "Weapons/RockLX"
	States
	{
	Spawn:
		GLGR A 0 A_CountDown
		GLGR A 1 A_CustomMissile("GrenadePuff", 3, 0, 0, 1)	 
		Loop
	Death:
		MISL B 0 Bright A_NoGravity
		MISL B 0 Bright A_SetTranslucent(0.5, 1)  
		MISL B 8 Bright A_Explode//(256, 160, 1)
		MISL C 6 Bright
		MISL D 4 BRIGHT
		Stop
	}
}

Actor GrenadePuff : BulletPuff
{
	States
	{
	Spawn:
		PUFF ABCD 4
		Stop
	}
}
