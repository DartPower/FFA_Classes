ACTOR Devastator : Weapon
{
	Weapon.AmmoType "MiniRockets"
	Weapon.AmmoGive 12
	Weapon.AmmoUse 1
	+INVENTORY.UNDROPPABLE
	Obituary "%k > Devastator > %o"
	States
	{
	Ready:
		DEV1 A 1 A_WeaponReady
		Loop
	Deselect:
		DEV1 A 1 A_Lower
		DEV1 AA 0 A_LOWER
		Loop
	Select:
		DEV1 A 1 A_Raise
		DEV1 AA 0 A_RAISE
		Loop
	Fire:
		DEV1 B 0 A_Light2
		DEV1 B 0 A_PlayWeaponSound("weapons/devshot")
		DEV1 AAAA 0 A_FireCustomMissile("DevastatorShot",random(2,-2),0,5,5)
		DEV1 B 1 A_FireCustomMissile("DevastatorShot",random(2,-2),1,5,6)
		DEV1 C 0 A_Light2
		DEV1 CEGHI 1 A_Light0
		DEV1 A 5 A_Light0
		Goto Ready
	}
}

ACTOR DoubleDevastator : Weapon
{
	Weapon.AmmoType "MiniRockets"
	Weapon.AmmoGive 12
	Weapon.AmmoUse 1
	+INVENTORY.UNDROPPABLE
	Obituary "%k > Double Devastator > %o"
	States
	{
	Ready:
		DEVG A 1 A_WeaponReady
		Loop
	Deselect:
		DEVG A 1 A_Lower
		DEVG AA 0 A_LOWER
		Loop
	Select:
		DEVG A 1 A_Raise
		DEVG AA 0 A_RAISE
		Loop
	Fire:
		NULL A 0 A_JumpIfInventory("Devcheck",1,"fire2")
		DEVG B 0 A_Light2
		DEVG B 0 A_PlayWeaponSound("weapons/devshot")
		DEVG AAAA 0 A_FireCustomMissile("DevastatorShot",random(2,-2),0,5,5)
		DEVG C 1 A_FireCustomMissile("DevastatorShot",random(2,-2),1,5,6)
		DEVG C 0 A_Light2
		DEVG EGHI 1 A_Light0 
		NULL A 0 A_GiveInventory("Devcheck",1)
		DEVG A 1 
		Goto Ready
	Fire2:
		DEVG B 0 A_Light2
		DEVG B 0 A_PlayWeaponSound("weapons/devshot")
		DEVG AAAA 0 A_FireCustomMissile("DevastatorShot",random(2,-2),0,-5,5)
		DEVG J 1 A_FireCustomMissile("DevastatorShot",random(2,-2),1,-5,6)
		DEVG C 0 A_Light2
		DEVG LNPQR 1 A_Light0 
		DEVG A 1 
		NULL A 0 A_TakeInventory("Devcheck",1)
		Goto Ready
	}
}

ACTOR Devcheck : Ammo
{
	Inventory.MaxAmount 1
}

ACTOR MiniRockets : Ammo 25048
{
	Inventory.Amount 6
	Inventory.MaxAmount 100
	Ammo.BackpackAmount 6
	Ammo.BackpackMaxAmount 200
	Inventory.PickupMessage "Picked up some mini rockets."
	Inventory.Icon MNRAA0
	States
	{
	Spawn:
		MNRA A -1
		Loop
	}
}

ACTOR DevastatorShot
{
	Radius 4
	Height 4
	Speed 18
	Scale 0.1
	Damage 12
	PROJECTILE
	ExplosionDamage 20
	ExplosionRadius 48
	SeeSound "weapons/devlnch"
	DeathSound "weapons/devexpl"
	States
	{
	Spawn:
		MNSS A 1 Bright A_SpawnItem("DevRockTrail")
		Loop
	Death:
		GRNE A 3 Bright A_Explode
		GRNE BCDEFGHIJKLMN 3 Bright
		Stop
	}
}

ACTOR MiniRocketBox : MiniRockets 25049
{
	+ALWAYSPICKUP
	Inventory.Amount 30
	Inventory.PickupMessage "Picked up a box of mini rockets."
	States
	{
	Spawn:
		MNRB A -1
		Loop
	}
}

ACTOR DevastatorGenerator : CustomInventory
{ 
	+FLOORCLIP
	+AUTOACTIVATE
	Scale 0.5
	Inventory.MaxAmount 2 
	Inventory.PickupSound "misc/w_pkup" 
	Inventory.PickupMessage "You got a Devastator" 
	States 
	{ 
	Spawn: 
		DVGG A -1 
		LOOP 
	Pickup: 
		TNT1 A 0 A_JumpIfInventory("DoubleDevastator", 1, 8) 
		TNT1 A 0 A_JumpIfInventory("Devastator", 1, 4) 
		TNT1 A 0 A_JumpIfInventory("DevastatorGenerator", 2, 6) 
		TNT1 A 0 A_GiveInventory("Devastator", 1) 
		TNT1 A 0 A_SelectWeapon("Devastator") 
		Stop 
		TNT1 A 0 A_TakeInventory("Devastator", 1) 
		TNT1 A 0 A_GiveInventory("DoubleDevastator", 1) 
		TNT1 A 0 A_SelectWeapon("DoubleDevastator") 
		Stop 
		TNT1 A 0 A_TakeInventory("DevastatorGenerator", 1) 
		TNT1 A 0 A_GiveInventory("miniRockets", 12) 
		Stop 
	} 
}

actor DevRockTrail
{
	Radius 1
	Height 1
	Speed 0
	Damage 0
	Scale 0.06
	RenderStyle Add
	Alpha 0.5
	+NOGRAVITY
	States
	{
	Spawn:
		PDBA VWXYZ 1 A_FadeOut(0.1)
		wait
	}
}

ACTOR Deviation : Weapon
{
	Weapon.UpSound "weapons/gemiup"
	Weapon.AmmoType "RocketAmmo"
	Weapon.AmmoGive 6
	Weapon.AmmoUse 1
	Scale 0.85
	+INVENTORY.UNDROPPABLE
	Obituary "%k > Deviation > %o"
	States
	{
	Ready: 
		RPRL A 1 A_WeaponReady
		LOOP
	Deselect:
		RPRL A 0 A_Lower
		RPRL A 1 A_Lower
		Loop
	Select: 
		RPRL A 0 A_Raise
		RPRL A 1 A_Raise
		Loop
	Fire:
		RPRL A 0 A_PlaySoundEx("weapons/gemifire","Auto")
		RPRL A 0 A_GunFlash
		RPRL BC 1 Bright
		RPRL C 0 A_FireCustomMissile("DumbRocket",random(3,-6),1,15,random(5,-5))
		RPRL DEF 1 Bright
		RPRL A 3
		Goto Ready
	Flash:
		TNT1 A 5 A_Light2
		TNT1 A 3 A_Light1
		TNT1 A 0 A_Light0
		Stop
	}
}

ACTOR DumbRocket : Rocket
{
	Speed 30
	-RANDOMIZE
	-ROCKETTRAIL
	SeeSound ""
	States
	{
	Spawn:
		MISL A 1 Bright A_SpawnItemEx("DumbTrail", 0, random(2,-2), random(2,-2), 0, 0, 0, 0, 128, 0)
		Loop
	Death:
		TNT1 A 0 A_Explode
		TNT1 AAAAAAAAAA 0 bright A_SpawnItemEx("Smoke", 0, 0, 0, random(2,-2), random(2,-2), 1+random(1,-3), 0, 128, 0)
		TNT1 AAAA 0 bright A_SpawnItemEx("Boomy", 0, random(2,-2), random(2,-2), 0, 0, 1, 0, 128, 0)
		TNT1 A 1
		Stop
	}
}

ACTOR Smoke
{ 
	Scale 0.5
	Alpha 0.55
	RenderStyle Add
	+NOCLIP
	+NOGRAVITY
	States
	{
	Spawn:
		SMOK ABCDEFGHIJKLMNOPQ 1 BRIGHT
		Stop
	}
}

ACTOR Boomy
{ 
	Scale 1
	Alpha 0.75
	RenderStyle Add
	+NOCLIP
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 0 A_Jump(255,"Spawn1","Spawn2")
	Spawn1:    
		EXP1 ABCDEFGHIJKLMNOP 1 BRIGHT
		Stop
	Spawn2:    
		EXP2 ABCDEFGHIJKLMNOP 1 BRIGHT
		Stop
	}
}

ACTOR DumbTrail : Smoke
{ 
	Scale 0.175
	Alpha 0.3
}
