Actor PlasmaPistol : Weapon
{
	Inventory.PickupSound "misc/w_pkup"
	Weapon.AmmoType "Plasma"
	Weapon.AmmoGive 20
	Weapon.Kickback 100
	Weapon.AmmoUse 1
	Decal RailGunScorch
	Inventory.PickupMessage "Picked up a Plasma Pistol"
	Obituary "%k > Plasma Pistol > %o"
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOAUTOAIM
	+INVENTORY.UNDROPPABLE
	States
	{
	Ready:
		PIS1 A 2 A_WeaponReady
		Loop
	Deselect:
		PIS1 A 0 A_Lower
		PIS1 A 1 A_Lower
		Loop
	Select:
		PIS1 A 0 A_Raise
		PIS1 A 1 A_Raise
		Loop
	Fire:
		PIS1 B 2 BRIGHT
		PIS1 C 2 BRIGHT A_FireCustomMissile("PlasmaBolt")
		PIS1 A 0 A_Recoil (1)
		PIS1 A 4
		PIS1 A 12 A_Recoil (-1)
		PIS1 A 0 A_Refire
		Goto Ready
	}
}

Actor PlasmaBolt : PlasmaBall
{
	States
	{
	Spawn:
		PLSS AB 6 Bright A_SpawnItemEx("PlasmaShaddow")
		Loop
	}
}

Actor PlasmaShaddow : PlasmaBall
{
	RenderStyle Translucent
	Alpha 0.35
	Speed 24
	States
	{
	Spawn:
		PLSS AB 6 Bright A_FadeOut(0.1)
		Stop
	}
}

Actor PulseRifle : Weapon
{
	Weapon.AmmoType "Plasma"
	Weapon.AmmoGive 50
	Weapon.AmmoUse 0
	+WEAPON.NOAUTOAIM
	+INVENTORY.UNDROPPABLE
	Decal "BulletChip"
	Obituary "%k > Pulse Rifle > %o"
	States
	{
	Deselect:
		TKGN A 0 SetPlayerProperty(0,0,2)
		TKGN A 0 A_Lower
		TKGN A 1 A_Lower
		Loop
	Select:
		TKGN A 0 SetPlayerProperty(0,1,2)
		TKGN A 0 A_Raise
		TKGN U 1 A_Raise
		Wait
	Ready:
		TKGN UVWXA 1
	RealReady:
		TKGN A 191 A_WeaponReady
		TKGN A 3 A_WeaponReady
		TKGN A 0 A_GiveInventory("Plasma",2)
		Goto RealReady+1
	Fire:
		TKGN A 1 A_JumpIfInventory("Plasma",1,"NormalShot")
	NoAmmo:
		TKGN A 20
		Goto Ready
	NormalShot:
		TKGN A 0 A_GunFlash
		TKGN A 0 A_PlayWeaponSound("PlasmaShot")
		TKGN A 0 A_TakeInventory("Plasma",1)
		TKGN A 4 A_FireCustomMissile("PlasmaBolt",0,1,5)
		TKGN A 0 A_TakeInventory("Plasma",1)
		TKGN A 4 A_FireCustomMissile("PlasmaBolt",0,1,5)
		Goto RealReady
	Flash:
		TKGN BC 4 bright
		Stop
	}
}

ACTOR CryoBow : Weapon
{
	Weapon.AmmoType "Plasma"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Obituary "%k > CryoBow > %o"
	+INVENTORY.UNDROPPABLE
	States
	{
	Deselect:
		BOWG A 0 A_Lower
		BOWG A 1 Bright A_Lower
		Loop
	Select:
		BOWG A 0 A_Raise
		BOWG A 1 Bright A_Raise
		Loop
	Ready:
		BOWG AAABBBCCCDDDBBB 1 Bright A_WeaponReady
    	Loop
	Fire:
		BOWG A 0 Bright A_GunFlash
		BOWG A 0 Bright A_Recoil(4.0)
		BOWG A 0 Bright A_PlaySound("weapons/cryobowshot")
		BOWG A 0 Bright A_PlaySound("weapons/cryobowfire", CHAN_WEAPON)
		BOWF A 2 Bright A_FireCustomMissile("CryoShot",0,1)
		BOWF BCD 2 Bright
		BOWF E 8
		BOWF E 2 A_PlaySound("weapons/cryobowreload")
		BOWF E 2 Offset(0,31) 
		BOWF F 2 Offset(0,32)
		BOWF F 2 Offset(0,30)
		BOWF G 2 Offset(0,31)
		BOWF HI 1 Offset(0,32)
		BOWG CDB 3 Bright Offset(0,32)
		BOWG A 0 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 2 Bright A_Light1
		TNT1 A 2 Bright A_Light2
		TNT1 A 1 Bright A_Light1
		Goto LightDone
	}
}

ACTOR CryoShot : FastProjectile
{
	Radius 4
	Height 8
	Speed 100
	Damage 0
	Projectile
	RenderStyle Add
	Alpha 0.9
	+NODAMAGETHRUST
	DeathSound "weapons/cryobowhit"
	MissileType "CryoTrail"
	MissileHeight 8
	Decal "PlasmaScorchLower"
	States
	{
	Spawn:
		BSHT A 0 Bright
		BSHT A 0 Bright A_PlaySound("weapons/cryobowflyby", CHAN_BODY, 1.0, 1)
		BSHT A 1 Bright
		Loop
	Death:		
		BXPL A 0 Bright A_StopSound(CHAN_BODY)
		BXPL A 0 Bright A_Explode(1, 128, 0)
		BXPL A 0 Bright A_ChangeFlag("ICEDAMAGE", 1)
		BXPL A 0 Bright A_ChangeFlag("NODAMAGETHRUST", 0)
		BXPL A 0 Bright A_Explode(50*random(1,4), 16, 0)
		BXPL A 0 Bright A_Explode(128, 128, 0)
		BXPL AAAAAAAA 0 Bright A_SpawnItemEx("CryoSmoke", 0, 0, 0, random(10, 30)*0.1, 0, random(0, 10)*0.1, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
		BXPL AAAA 0 Bright A_SpawnItemEx("CryoSmoke2", 0, 0, 0, random(10, 30)*0.04, 0, random(0, 10)*0.04, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,64)
		BXPL ABCDEFGH 1 Bright
		BXPL IJKLLM 1 Bright A_FadeOut(0.1)
		Stop
	}
}

ACTOR CryoTrail
{
	+NOINTERACTION
	Radius 4
	Height 8
	Renderstyle Add
	Alpha 0.5
	States
	{
	Spawn:
		BSHT AABBCDEFG 1 Bright
		Stop
	}
}

ACTOR CryoSmoke
{
    Radius 2
    Height 2
    +NOINTERACTION
    +FORCEXYBILLBOARD
    RenderStyle Shaded
    StencilColor "A0 A0 A0"
    Alpha 1.0
    Scale 0.4
    States
    {
    Spawn:
		SMOK A 0
        SMOK A 2 A_FadeOut (0.05)
        Wait
    }
}

Actor CryoSmoke2 : CryoSmoke { StencilColor "DD DD DD" Scale 0.7 Alpha 0.5 VSpeed 2.5
	States {
	Spawn:
		TNT1 A 0
		SMOK A 6 A_FadeOut(0.04)
		Wait
	}
}