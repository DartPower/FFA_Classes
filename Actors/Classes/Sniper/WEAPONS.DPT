Actor SniperRifle: Weapon
{
	Obituary "%o was sniped by %k's sniper rifle"
	Inventory.PickupMessage "You got The Sniper Rifle!"
	Inventory.PickupSound "misc/pk_up"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 10
	Weapon.AmmoType "SniperAmmo"
	+INVENTORY.UNDROPPABLE
	+NOALERT
	States
	{
	Ready:
		SNIE JIHGFEDCBA 1
		SNIE A 1 A_WeaponReady
		Goto Ready+10
	Deselect:
		SNIE B 1 A_Lower
		Loop
	Select:
		SNIE A 0 A_Raise
		Loop
	Fire:
		SNIF A 0 A_AlertMonsters
		SNIF A 0 A_PlayWeaponSound("Weapon/SniperFire")
		SNIF A 0 A_GunFlash
		SNIF A 6 A_FireCustomMissile("Projectile",0,6,0)
		SNIR A 2 A_FireCustomMissile("BulletCasing",0,1,8)
		SNIR BCDEFGHIJ 2
		SNIR J 2 A_PlayWeaponSound("Weapon/SniperCharge")
		SNIR IHGFEDCBA 2
		SNIR A 0 A_Refire
		Goto Ready+10
	AltFire:
		SNIR A 0 A_GiveInventory("SniperRifleZoomed")
		SNIR A 0 A_SelectWeapon("SniperRifleZoomed")
		Goto Deselect
	Flash:
		SNIF F 3 Bright A_Light2
		SNIF F 3 Bright A_Light1
		SNIF F 0 Bright A_Light0
		Goto LightDone
	}
}

Actor SniperRifleZoomed : Weapon
{
	Weapon.Ammouse 1
	Weapon.AmmoType "SniperAmmo"
	+CHEATNOTWEAPON
	+NOALERT
	+DONTBOB
	States
	{
	Ready:
		RIFL A 0 Bright A_ZoomFactor(3.0)
		RIFL A 1 Bright A_WeaponReady
		Goto Ready+1
	Deselect:
		RIFL A 0 Bright A_ZoomFactor(1.0)
		TNT1 A 1 Bright A_Lower
		Goto Deselect+1
	Select:
		RIFL A 0 Bright A_Raise
		Loop
	Fire:
		RIFL A 0 Bright A_AlertMonsters
		RIFL A 0 Bright A_PlayWeaponSound("Weapon/SniperFire")
		RIFL A 6 Bright A_FireCustomMissile("Projectile",0,6,0)
		RIFL A 20 Bright A_FireCustomMissile("BulletCasing",0,1,8)
		RIFL A 20 Bright A_PlayWeaponSound("Weapon/SniperCharge")
		RIFL A 0 Bright A_Refire
		Goto Ready+1
	AltFire:
		RIFL A 0 Bright A_ZoomFactor(1.0)
		RIFL A 0 Bright A_SelectWeapon("SniperRifle")
		Goto Deselect
	Flash:
		TNT1 A 3 Bright A_Light2
		TNT1 A 3 Bright A_Light1
		TNT1 A 0 Bright A_Light0
		Goto LightDone
	}
}

Actor Projectile
{
	Scale 0.50
	Height 5
	Radius 13
	Damage 30
	Speed 50
	Projectile
	+NOEXTREMEDEATH
	+MISSILE
	+DROPOFF
	Decal "bulletchip"
	States
	{
	Spawn:
		PROJ A 1
		Loop
	Death:
		TNT1 A 0 A_SetTranslucent(0.5,0)
		PUFF ABCD 4
		Stop
	}
}

Actor BulletCasing
{
	Height 12
	Radius 8
	Speed 4
	PROJECTILE
	-NOGRAVITY
	+LOWGRAVITY
	+NOTELEPORT
	+DONTSPLASH
	-NOBLOCKMAP
	States
	{
	Spawn:
		BULL ABCDEF 3 Bright
		Loop
	Death:
		BULL C -1
		Loop
	}
}
