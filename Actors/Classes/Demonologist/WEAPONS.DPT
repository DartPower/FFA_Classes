//WEAPONS.DPT
//LUMP VERSION NUMBER 0.73

ACTOR Unmaker : Weapon
{
	AttackSound "weapons/unmaker"
	Decal "BloodSplat"
	Inventory.PickupMessage "You got the Unmaker!"
	Inventory.PickupSound "misc/w_pkup"
	Obituary "%o felt the dark forces behind %k's Unmaker"
	Tag "Unmaker"
	Weapon.AmmoGive 90
	Weapon.AmmoType1 "Magic"
	Weapon.AmmoType2 "Magic"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 4
	Weapon.Kickback 1
	Weapon.SelectionOrder 650
	+EXTREMEDEATH
	+WEAPON.ALT_AMMO_OPTIONAL
	+WEAPON.AMMO_OPTIONAL
	States
	{
	Spawn:
		ABCD E -1
		Stop
	Ready:
		UNMK A 0 A_JumpIfInventory("ChiGem", 1, 2)
		UNMK A 1 A_WeaponReady
		Loop
		UNMK A 0 A_JumpIfInventory("Magic", 0, 3)
		UNMK A 0 A_GiveInventory("Magic", 1)
		UNMK A 0 A_PlaySound("weapons/unmakercharge")
		UNMK AAAAA 1 A_WeaponReady(WRF_NOSECONDARY)
		Loop
	Deselect:
		UNMK A 1 A_Lower
		Loop
	Select:
		UNMK A 1 A_Raise
		Loop
	Fire:
		UNMK BC 4
	Hold:
		UNMK A 0 A_JumpIfInventory("ChiGem", 1, "HoldPowerd")
		UNMK A 0 A_JumpIfNoAmmo(10)
		UNMK D 3 Bright A_FireBullets(2, 2, 1, 30, "BloodyPuff", 1, 15360)
		UNMK EF 3
		UNMK CCCCCC 1 A_Refire
		UNMK BA 4
		Goto Ready
	HoldPowerd:
		UNMK A 0 A_JumpIfNoAmmo(25)
		UNMK A 0 A_PlaySound("weapons/flash")
		UNMK D 1 Bright A_RailAttack(25, 0, 1, "FF 00 00", none, 0)
		UNMK D 1 Bright A_RailAttack(25, 0, 1, "CC 00 00", none, 0)
		UNMK D 1 Bright A_RailAttack(25, 0, 1, "99 00 00", none, 0)
		UNMK D 1 Bright A_RailAttack(25, 0, 1, "55 00 00", none, 0)
		UNMK D 1 Bright A_RailAttack(25, 0, 1, "33 00 00", none, 0)
		UNMK O 2 Bright A_Recoil(2)
		UNMK GHIJKLMN 2
		UNMK C 6
		UNMK CCCCCC 1 A_Refire
		Goto HoldPowerd
		UNMK BA 4
		Goto Ready
		UNMK BA 4
		Goto Ready+5
	AltFire:
		UNMK BC 4
	AltHold:
		UNMK A 0 A_JumpIfNoAmmo(16)
		UNMK D 3 Bright A_FireBullets(4, 4, 1, 70, "BloodyPuff", 1)
		UNMK HIJKLMN 2
		UNMK C 6
		UNMK CCCCCC 1 A_Refire
		UNMK BA 4
		Goto Ready
	}
}

ACTOR BloodyPuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	States
	{
	Spawn:
		DBLD A 4 Bright
	Crash:
		DBLD BCD 4 A_SpawnItemEx("FriendDoomImp",0,0,0,0,0,SXF_NOCHECKPOSITION | SXF_TELEFRAG)
		Stop
	}
}

Actor FriendDoomImp
{
	Health 15
	Radius 20
	Height 56
	Mass 100
	Speed 8
	PainChance 255
	MeleeDamage 3
	Monster
	+FLOORCLIP
	+FRIENDLY
	SeeSound "imp/sight"
	PainSound "imp/pain"
	DeathSound "imp/death"
	ActiveSound "imp/active"
	HitObituary "$OB_IMPHIT"
	Obituary "$OB_IMP"
	States
	{
	Spawn:
		TROO AB 10 A_Look
		Loop
	See:
		TROO AABBCCDD 3 A_Chase
		Loop
	Missile:
	Melee:
		TROO EF 8 A_FaceTarget
		TROO G 6 A_MeleeAttack
		Goto See
	Pain:
		TROO H 2
		TROO H 2 A_Pain
		Goto See
	Death:
		TROO I 8
		TROO J 8 A_Scream
		TROO K 6
		TROO L 6 A_NoBlocking
		TROO M -1
		Stop
	XDeath:
		TROO N 5
		TROO O 5 A_XScream
		TROO P 5
		TROO Q 5 A_NoBlocking
		TROO RST 5
		TROO U -1
		Stop
	Raise:
		TROO MLKJI 8
		Goto See
	}
}